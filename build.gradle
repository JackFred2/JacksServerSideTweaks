plugins {
	id 'fabric-loom' version '1.1-SNAPSHOT'
	id 'io.github.juuxel.loom-quiltflower' version '1.8.0'

	id 'com.matthewprenger.cursegradle' version '1.4.0'
	id "com.modrinth.minotaur" version "2.+"
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = "${project.archives_base_name}-${project.minecraft_version}"
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		name = "Shedaniel Maven"
		url = "https://maven.shedaniel.me"
		content {
			includeGroupByRegex("me.shedaniel.*")
			includeGroupByRegex("dev.architectury.*")
		}
	}

	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com"
		content {
			includeGroupByRegex("com.terraformersmc.*")
			includeGroup("dev.emi")
		}
	}

	maven {
		name = "ParchmentMC mappings"
		url = "https://maven.parchmentmc.org/"
		content {
			includeGroupByRegex("org.parchmentmc.*")
		}
	}

	maven {
		url = "https://server.bbkr.space/artifactory/libs-release"
		content {
			includeGroupByRegex("io.github.cottonmc.*")
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-${project.parchment_version}@zip")
	}
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}"
	// modLocalRuntime "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"

	//modCompileOnly "dev.emi:emi:${project.emi_version}:api"
	//modLocalRuntime "dev.emi:emi:${project.emi_version}"

	modImplementation "io.github.cottonmc:Jankson-Fabric:${project.jankson_version}"
	include "io.github.cottonmc:Jankson-Fabric:${project.jankson_version}"

	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

loom {
	accessWidenerPath = file("src/main/resources/jsst.accesswidener")
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

curseforge {
	if (project.hasProperty("cf.key")) {
		apiKey = project.findProperty("cf.key")
		project {
			id = "850725"
			changelog = "Check the GitHub for changes: https://github.com/JackFred2/JacksServerSideTweaks"
			releaseType = "beta"

			addGameVersion "Fabric"
			addGameVersion "Quilt"
			addGameVersion "Java 17"

			project.game_versions.tokenize(",").each { addGameVersion it }

			mainArtifact(remapJar) {
				relations {
					requiredDependency("fabric-api")
				}
				displayName = "${project.prefix} ${archivesBaseName}-${version}.jar"
			}

		}

		options {
			forgeGradleIntegration = false
		}
	}
}

modrinth {
	token = project.findProperty("modrinth.key")
	projectId = "eiYW74Yj"
	versionNumber = "mc${project.minecraft_version}-${project.mod_version}"
	versionType = "beta"
	uploadFile = remapJar
	changelog = "Check the GitHub for changes: https://github.com/JackFred2/JacksServerSideTweaks"
	gameVersions = project.game_versions.tokenize(",")
	loaders = ["fabric", "quilt"]
	syncBodyFrom = rootProject.file("README.md").text
	dependencies {
		required.project "fabric-api"
	}
}

tasks.modrinth.dependsOn(tasks.modrinthSyncBody)

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
	}
}
